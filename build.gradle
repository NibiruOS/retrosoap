buildscript {
    repositories {
        mavenCentral()
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url "https://oss.sonatype.org/content/groups/staging/"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/google-snapshots/"
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

project.ext {
    nibiruId = 'retrosoap'
}

version = '0.1-SNAPSHOT'
group = 'io.github.nibiruos.retrosoap'

sourceCompatibility = 1.7

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

tasks.withType(Javadoc) {
    // Workaround for https://github.com/gradle/gradle/issues/5630
    (options as CoreJavadocOptions).addStringOption("sourcepath", "")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'Nibiru ' + project.name
                description = 'Retrofit based SOAP client, compatible with Android.'
                url = 'https://github.com/NibiruOS/' + nibiruId + '/tree/master/' + project.name
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'lbrasseur'
                        name = 'Lautaro Brasseur'
                        email = 'lbrasseur@gmail.com'
                    }
                }
                scm {
                    connection = 'https://github.com/NibiruOS/' + nibiruId + '.git'
                    developerConnection = 'https://github.com/NibiruOS/' + nibiruId + '.git'
                    url = 'https://github.com/NibiruOS/' + nibiruId
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username = sonatypeUser
                password = sonatypePassword
            }
            url version.endsWith('SNAPSHOT') ? 'https://oss.sonatype.org/content/repositories/snapshots' : 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc.classpath = sourceSets.main.compileClasspath

def retrofitVersion = "2.4.0"

dependencies {
    compile "com.squareup.retrofit2:retrofit:${retrofitVersion}";
    compile("com.squareup.retrofit2:converter-simplexml:${retrofitVersion}") {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'stax', module: 'stax'
        exclude group: 'xpp3', module: 'xpp3'
    }
    compile('org.simpleframework:simple-xml:2.7.+') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
    compile "javax.inject:javax.inject:1"
	compile "com.google.code.findbugs:jsr305:2.0.3"
    compile 'net.sf.sociaal:xmlpull-xpp3:3.0.0.20130526'
    testCompile 'junit:junit:4.12'
    testCompile "org.easymock:easymock:3.6"
}